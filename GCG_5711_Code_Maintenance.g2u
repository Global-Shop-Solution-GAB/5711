Program.Sub.ScreenSU.Start
Gui.Form..Create
Gui.Form..Size(4725,10395)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form..Position(0,0)
Gui.Form..BackColor(-2147483633)
Gui.Form..MousePointer(0)
Gui.Form..Event(UnLoad,Form_UnLoad)
Gui.Form..Caption("Codes")
Gui.Form.frame1.Create(Frame)
Gui.Form.frame1.Size(4320,1455)
Gui.Form.frame1.Position(90,75)
Gui.Form.txtCode.Create(TextBox,"",True,600,300,0,135,480,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtCode.Parent("frame1")
Gui.Form.txtCode.MaxLength(4)
Gui.Form.txtCode.Mask(">>>>")
Gui.Form.txtDescription.Create(TextBox,"",True,3315,300,0,930,480,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtDescription.Parent("frame1")
Gui.Form.txtDescription.MaxLength(30)
Gui.Form.lbl1.Create(Label,"Code",True,585,255,0,105,240,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl1.Parent("frame1")
Gui.Form.lbl2.Create(Label,"Description",True,1455,255,0,945,240,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl2.Parent("frame1")
Gui.Form.cmdAdd.Create(Button)
Gui.Form.cmdAdd.Size(855,375)
Gui.Form.cmdAdd.Position(3375,900)
Gui.Form.cmdAdd.Parent("frame1")
Gui.Form.cmdAdd.Caption("Add")
Gui.Form.cmdAdd.Event(Click,cmdAdd_Click)
Gui.Form.cmdDelete.Create(Button)
Gui.Form.cmdDelete.Size(855,375)
Gui.Form.cmdDelete.Position(2460,900)
Gui.Form.cmdDelete.Parent("frame1")
Gui.Form.cmdDelete.Caption("Delete")
Gui.Form.cmdDelete.Event(Click,cmdDelete_Click)
Gui.Form.GsGcDisplay.Create(GsGridControl)
Gui.Form.GsGcDisplay.Size(4320,7710)
Gui.Form.GsGcDisplay.Position(90,1590)
Gui.Form.GsGcDisplay.Event(RowCellClick,GsGcDisplay_RowCellClick)
Gui.Form.cmdUpload.Create(Button)
Gui.Form.cmdUpload.Size(855,375)
Gui.Form.cmdUpload.Position(3570,9375)
Gui.Form.cmdUpload.Caption("Upload")
Gui.Form.cmdUpload.Event(Click,cmdUpload_Click)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
f.Intrinsic.Control.Try
	f.Intrinsic.Control.CallSub(LoadIcon)
	f.Intrinsic.Control.CallSub(createDisplayDt)
	f.Intrinsic.Control.CallSub(loadGridProperties)
	f.Intrinsic.Control.CallSub(loadDatatable)
	gui.Form..Show
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.ErrorMsg.Start
v.Local.sError.Declare

'Closes ODBC connection, default to connection : "con"
f.Intrinsic.Control.If(v.ODBC!con.State, =, 1)
	f.ODBC.Connection!con.Close
f.Intrinsic.Control.EndIf

'Generic Error message.
f.Intrinsic.String.Build("Project GCG_5711_Code_Maintenance.g2u {0}{0}Subroutine: {1}{0}Error: {2} with Description: {3}", v.Ambient.NewLine, v.Args.CurrentSub, v.Ambient.ErrorNumber, v.Ambient.ErrorDescription, v.Local.sError)
f.Intrinsic.UI.Msgbox(v.Local.sError)
Program.Sub.ErrorMsg.End

'f.Intrinsic.Control.CallSub(createDisplayDT)
Program.Sub.createDisplayDT.Start
f.Intrinsic.Control.Try
	f.Data.DataTable.Create("displayDT", true)
	f.Data.DataTable.AddColumn("displayDT", "CODE", "String")
	f.Data.DataTable.AddColumn("displayDT", "DESCRIPTION", "String")
	f.Data.DataTable.AddColumn("displayDT", "GSS_USER", "String")
	f.Data.DataTable.AddColumn("displayDT", "GSS_DATE", "String")
	
	f.Data.DataView.Create("displayDT", "displayDV", 22)
	gui.Form.GsGcDisplay.AddGridviewFromDataview("displayGV", "displayDT", "displayDV")
	gui.Form.GsGcDisplay.MainView("displayGV")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.createDisplayDT.End

Program.Sub.loadGridProperties.Start
v.Local.iCnt.Declare
v.Local.sFields.Declare
f.Intrinsic.Control.Try
	f.Intrinsic.String.Split(v.DataTable.displayDT.FieldNames, "*!*", v.Local.sFields)
	f.Intrinsic.Control.For(v.Local.iCnt, 0, v.Local.sFields.UBound, 1)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderFontBold", True)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "Visible", True)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "ReadOnly", True)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "AllowEdit", False)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderHAlignment", "Near")
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "CellHAlignment", "Near")
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderBackColor", v.Color.GSS)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderForeColor", v.Color.White)
	f.Intrinsic.Control.Next(v.Local.iCnt)
	
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "CODE", "caption", "Code")
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "DESCRIPTION", "caption", "Description")
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "GSS_USER", "caption", "User")
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "GSS_DATE", "caption", "Date")
	
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "CODE", "HeaderHAlignment", "Center")
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "CODE", "CellHAlignment", "Center")
	
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "CODE", "Minwidth", 40)
	
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "GSS_USER", "visible", false)
	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "GSS_DATE", "visible", false)
	
	gui.Form.GsGcDisplay.BestFitColumns("displayGV")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.loadGridProperties.End

Program.Sub.loadDatatable.Start
f.Intrinsic.Control.Try
	f.ODBC.Connection!con.OpenCompanyConnection
	 	f.Data.DataTable.CreateFromSQL("tmpDisplayDT", "con", "Select * from GCG_5711_CUSTOM_CODE")
	f.ODBC.Connection!con.Close
	
	f.Data.DataTable.DeleteRow("displayDT")
	f.Data.DataTable.AcceptChanges("displayDT")
	f.Data.DataTable.Merge("tmpDisplayDT", "displayDT", false, 2)
	f.Data.DataTable.Close("tmpDisplayDT")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.loadDatatable.End

Program.Sub.cmdDelete_Click.Start
v.Local.sCode.Declare
v.Local.sSQL.Declare
f.Intrinsic.Control.Try
	v.Local.sCode.Set(v.Screen.Form!txtCode.Text!FieldValTrim)
	'Delete using Code as PK.
	f.Intrinsic.String.Build("Delete from GCG_5711_CUSTOM_CODE where Code ='{0}'", v.Local.sCode, v.Local.sSQL)
	f.ODBC.Connection!con.OpenCompanyConnection
	 	f.ODBC.Connection!con.Execute(v.Local.sSQL)
	f.ODBC.Connection!con.Close
	
	gui.Form.txtCode.Text("")
	gui.Form.txtDescription.Text("")
	f.Intrinsic.Control.CallSub(loadDatatable)	
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdDelete_Click.End

Program.Sub.cmdAdd_Click.Start
v.Local.sCode.Declare
v.Local.sDescription.Declare
v.Local.sRet.Declare
v.Local.sSQL.Declare
f.Intrinsic.Control.Try
	v.Local.sCode.Set(v.Screen.Form!txtCode.Text)
	v.Local.sDescription.Set(v.Screen.Form!txtDescription.Text)
	
	'Check if the Code is in use.
	f.Intrinsic.String.Build("Select Code from GCG_5711_CUSTOM_CODE where Code = '{0}'", v.Local.sCode, v.Local.sSQL)
	f.ODBC.Connection!con.OpenCompanyConnection
	 	f.ODBC.Connection!con.ExecuteAndReturn(v.Local.sSQL, v.Local.sRet)
	f.ODBC.Connection!con.Close
	
	f.Intrinsic.Control.If(v.Local.sRet.Trim, =, v.Local.sCode.Trim)
		f.Intrinsic.UI.Msgbox("This code is already in use. Please input a new one.")
	f.Intrinsic.Control.Else
		'Add using code as PK.
		f.Intrinsic.String.Build("Insert into  GCG_5711_CUSTOM_CODE (Code, Description, GSS_USER, GSS_DATE) Values ('{0}', '{1}', '{2}', '{3}')", v.Local.sCode.Trim, v.Local.sDescription.Trim, v.Caller.User.Trim, v.Ambient.Date.PervasiveDate, v.Local.sSQL)
		f.ODBC.Connection!con.OpenCompanyConnection
		 	f.ODBC.Connection!con.Execute(v.Local.sSQL)
		f.ODBC.Connection!con.Close
	f.Intrinsic.Control.EndIf	
	
	f.Intrinsic.Control.CallSub(loadDatatable)
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdAdd_Click.End

Program.Sub.cmdUpload_Click.Start
V.Local.sFilePath.Declare
v.Local.sText.Declare
f.Intrinsic.Control.Try
	'Get File Location
	f.Intrinsic.UI.ShowOpenFileDialog("", "", "", v.Local.sFilePath)
	f.Intrinsic.Control.If(v.Local.sFilePath.Trim, =, "***CANCEL***")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf
	'Get Text from File.
	f.Intrinsic.File.File2String(v.Local.sFilePath, v.Local.sText)
	'Create Save to Datatable.
	f.Data.DataTable.CreateFromString("saveDisplayDT", v.Local.sText, "code*!*description", "String*!*String", ",", v.Ambient.NewLine)
	f.Data.DataTable.AddColumn("saveDisplayDT", "GSS_USER", "String", v.Caller.User.Trim)
	f.Data.DataTable.AddColumn("saveDisplayDT", "GSS_DATE", "String",  v.Ambient.Date.PervasiveDate)
	
	f.Data.DataTable.SetValueFormat("saveDisplayDT", -1, "Code", "Code", ">>>>")
	'Save to DB
	f.ODBC.Connection!con.OpenCompanyConnection
	 	f.Data.DataTable.SaveToDB("saveDisplayDT", "con", "GCG_5711_CUSTOM_CODE", "Code", 256)
	f.ODBC.Connection!con.Close
	
	f.Data.DataTable.Close("saveDisplayDT")
	'Reload the Data.
	f.Intrinsic.Control.CallSub(loadDatatable)
	
	f.Intrinsic.UI.Msgbox("Upload Complete.")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdUpload_Click.End

Program.Sub.Form_UnLoad.Start
f.Intrinsic.Control.Try
	f.Intrinsic.Control.End
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.Form_UnLoad.End

Program.Sub.GsGcDisplay_RowCellClick.Start
v.Local.sCode.Declare
v.Local.sDescription.Declare
f.Intrinsic.Control.Try
	v.Local.sCode.Set(v.DataTable.displayDT(v.Args.RowIndex).Code!FieldValTrim)
	v.Local.sDescription.Set(v.DataTable.displayDT(v.Args.RowIndex).Description!FieldValTrim)
	'Set selected row in the Txtboxes.
	gui.Form.txtCode.Text(v.Local.sCode)
	gui.Form.txtDescription.Text(v.Local.sDescription)
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.GsGcDisplay_RowCellClick.End

Program.Sub.loadIcon.Start
v.Local.sIcon.Declare
'sets the icon
f.Intrinsic.String.Build("{0}\ART\gss2.ico", v.Caller.GlobalDir, v.Local.sIcon)
gui.Form..Icon(v.Local.sIcon)
Program.Sub.loadIcon.End
